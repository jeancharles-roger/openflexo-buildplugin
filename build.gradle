plugins {
    id "com.jfrog.artifactory" version "4.6.2"
}

subprojects {
    group 'org.openflexo'

    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.artifactory'

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    artifactory {
        contextUrl = 'https://maven.openflexo.org/artifactory'
        publish {
            repository {
                repoKey = 'openflexo-snapshot' // The Artifactory repository key to publish to
                username = "$System.env.ARTIFACTORY_USER" // The publisher user name
                password = "$System.env.ARTIFACTORY_PASSWORD" // The publisher password
            }
            defaults {
                // Reference to Gradle publications defined in the build script.
                // This is how we tell the Artifactory Plugin which artifacts should be
                // published to Artifactory.
                publications('mavenJava')
                publishArtifacts = true
                // Properties to be attached to the published artifacts.
                //properties = ['qa.level': 'basic', 'dev.team' : 'core']
                // Publish generated POM files to Artifactory (true by default)
                publishPom = true
            }
        }
        resolve {
            repoKey = 'maven'
        }
    }

    dependencies {
        compile gradleApi()
        compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.12'
    }

}
